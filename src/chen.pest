// 隐式规则：自动忽略空格和注释（但不包括换行）
WHITESPACE = _{ " " | "\t" | "\r" }
NEWLINE = _{ "\n" }
// 注释：# 后面不能是 { (避免和对象字面量 #{ 冲突)
// 注意：注释不吞掉换行符，留给 stmt_sep 处理
COMMENT = _{ "#" ~ !"{" ~ (!NEWLINE ~ ANY)* }

// 关键字
LET = { "let" }
FOR = { "for" }
DEF = { "def" }
RETURN = { "return" }
IF = { "if" }
ELSE = { "else" }
TRUE = { "true" }
FALSE = { "false" }
BREAK = { "break" }
CONTINUE = { "continue" }
NULL = { "null" }

// 标识符 (不能是关键字)
identifier = @{ !keyword ~ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
keyword = { LET | FOR | DEF | RETURN | IF | ELSE | TRUE | FALSE | BREAK | CONTINUE | NULL }

// 字面量
integer = @{ "-"? ~ ASCII_DIGIT+ }
float = @{ "-"? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }
string = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" | "'" ~ (!"'" ~ ANY)* ~ "'" }
bool = { TRUE | FALSE }

// 操作符
add = { "+" }
subtract = { "-" }
multiply = { "*" }
divide = { "/" }
modulo = { "%" }
assign = { "=" }
eq = { "==" }
neq = { "!=" }
lt = { "<" }
lte = { "<=" }
gt = { ">" }
gte = { ">=" }
and = { "&&" }
or = { "||" }
not = { "!" }

// 表达式优先级处理
expression = { logical_or }

logical_or = { logical_and ~ (or ~ logical_and)* }
logical_and = { equality ~ (and ~ equality)* }
equality = { comparison ~ ((eq | neq) ~ comparison)* }
comparison = { term ~ ((lte | lt | gte | gt) ~ term)* }
term = { factor ~ ((add | subtract) ~ factor)* }
factor = { unary ~ ((multiply | divide | modulo) ~ unary)* }
unary = { (not | subtract) ~ unary | primary }

primary = { atom ~ postfix* }

atom = {
    float |
    integer |
    bool |
    string |
    identifier |
    "(" ~ expression ~ ")" |
    if_expr |
    block |
    object_literal
}

postfix = { call_suffix | dot_suffix | index_suffix }
call_suffix = { "(" ~ arguments? ~ ")" }
dot_suffix = { "." ~ identifier }
index_suffix = { "[" ~ expression ~ "]" }

object_literal = { "#{" ~ NEWLINE* ~ (pair ~ ("," ~ NEWLINE* ~ pair)*)? ~ NEWLINE* ~ "}" }
pair = { (identifier | integer) ~ ":" ~ expression }

// 语句分隔符 - 使用换行
stmt_sep = _{ NEWLINE+ }

// 语句
statement = {
    declaration |
    for_loop |
    function_def |
    return_stmt |
    break_stmt |
    continue_stmt |
    assignment |
    expression
}

declaration = { LET ~ identifier ~ assign ~ expression }
assignment_target = { identifier ~ postfix* } 
assignment = { assignment_target ~ assign ~ expression }

for_loop = { FOR ~ expression ~ block }
function_def = { DEF ~ identifier ~ "(" ~ parameters? ~ ")" ~ block }
return_stmt = { RETURN ~ expression }
break_stmt = { BREAK }
continue_stmt = { CONTINUE }

// 复合结构
block = { "{" ~ NEWLINE* ~ (statement ~ stmt_sep)* ~ statement? ~ NEWLINE* ~ "}" }
if_expr = { IF ~ expression ~ block ~ (ELSE ~ block)? }
parameters = { identifier ~ ("," ~ identifier)* }
arguments = { expression ~ ("," ~ expression)* }

// 程序入口 - 允许语句之间有换行
program = { SOI ~ NEWLINE* ~ (statement ~ stmt_sep)* ~ statement? ~ NEWLINE* ~ EOI }
